// Gradle settings and tasks common to all Acorus subprojects

apply plugin: 'java'

ext {
    group = 'com.github.stephengold'
    websiteUrl = 'https://github.com/stephengold/Acorus'

    // module coordinates of dependencies:
    heartCoordinates = 'com.github.stephengold:Heart:7.4.0'

    // current versions of libraries:
    acorusVersion = '0.9.10-SNAPSHOT'
    jme3Version = '3.5.0-stable'
    lwjgl3Version = '3.3.0'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true // to provide detailed deprecation warnings
    options.encoding = 'UTF-8'
}

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform;
Boolean isMacOS = DefaultNativePlatform.currentOperatingSystem.isMacOsX();

tasks.withType(JavaExec) { // Java runtime options:
    if (isMacOS) {
        jvmArgs '-XstartOnFirstThread'
    } else {
        args '--showSettingsDialog'
    }
    //args '--verbose' // to enable additional log output
    classpath sourceSets.main.runtimeClasspath
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xbatch'
    //jvmArgs '-Xms512m', '-Xmx512m'
    //jvmArgs '-XX:+PrintCompilation'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

repositories {
    //mavenLocal() // to find local SNAPSHOTs of libraries
    mavenCentral()
}
